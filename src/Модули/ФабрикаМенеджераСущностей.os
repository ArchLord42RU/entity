#Использовать asserts
#Использовать reflector

Перем ИнтерфейсМенеджерСущностей;

Функция ПолучитьМенеджерСущностей(ТипМенеджераСущностей) Экспорт

	МенеджерСущностей = Новый(ТипМенеджераСущностей);
	
	ПроверитьПоддержкуИнтерфейсаМенеджераСущностей(МенеджерСущностей);
	
	МенеджерСущностей.Открыть();
	
	Возврат МенеджерСущностей;
	
КонецФункции

Процедура ПроверитьПоддержкуИнтерфейсаМенеджераСущностей(МенеджерСущностей)
	
	РефлекторОбъекта = Новый РефлекторОбъекта(МенеджерСущностей);
	ПоддерживаетсяИнтерфейсМенеджераСущностей = РефлекторОбъекта.РеализуетИнтерфейс(ИнтерфейсМенеджерСущностей);
	
	Ожидаем.Что(
		ПоддерживаетсяИнтерфейсМенеджераСущностей,
		СтрШаблон("Тип <%1> не реализует интерфейс менеджера сущностей", ТипЗнч(МенеджерСущностей))
	).ЭтоИстина();

КонецПроцедуры

ИнтерфейсМенеджерСущностей = Новый ИнтерфейсОбъекта;
ИнтерфейсМенеджерСущностей
	.ПроцедураИнтерфейса("Открыть")
	.ПроцедураИнтерфейса("НачатьТранзакцию")
	.ПроцедураИнтерфейса("ЗафиксироватьТранзакцию")
	.ПроцедураИнтерфейса("ИнициализироватьТаблицу", 1)
	.ПроцедураИнтерфейса("Сохранить", 1)
	.ПроцедураИнтерфейса("Закрыть");